<?xml version="1.0" encoding="utf-8"?>
<mx:VDividedBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
				color="black"
				label="Actionscript" xmlns:panels="com.flexspy.parser.view.panels.*" xmlns:view="com.flexspy.parser.view.*"
				creationComplete="onCreationComplete()">
	<mx:Script>
		<![CDATA[
			import mx.effects.Effect;
			import mx.effects.Glow;
			import com.flexspy.FlexSpy;
			import com.flexspy.event.FlexSpyEvent;
			import com.flexspy.parser.model.FlexTrampModelLocator;
			
			import mx.core.Application;
			
			private function onCreationComplete() : void{
				FlexTrampModelLocator.getInstance().parser.addVariable("app", Application.application );
				//register for flexspy events
				FlexSpy.addEventListener(FlexSpyEvent.HIDE, handleFlexSpyEvent);
				FlexSpy.addEventListener(FlexSpyEvent.SHOW, handleFlexSpyEvent);
				FlexSpy.addEventListener(FlexSpyEvent.PROPERTY_SELECTED, handleFlexSpyEvent);
				FlexSpy.addEventListener(FlexSpyEvent.VIEW_ITEM_SELECTED, handleFlexSpyEvent);
			}
			
			private function handleFlexSpyEvent(event : FlexSpyEvent) : void{
				if ( !wasPressingAlt(event.associatedEvent)) return;
				
				if ( event.type == FlexSpyEvent.PROPERTY_SELECTED){
					FlexTrampModelLocator.getInstance().parser.addVariable( event.propertyName, event.propertyValue);
					highlightComponentEffect.play([ event.associatedEvent.target ]);
				} else if (event.type == FlexSpyEvent.VIEW_ITEM_SELECTED){
					FlexTrampModelLocator.getInstance().parser.addVariable( event.viewItem.name, event.viewItem );
					highlightComponentEffect.play([ event.associatedEvent.target ]);
				}
			}
			
			private function wasPressingAlt(event : Event) : Boolean{
				return event is MouseEvent && MouseEvent(event).altKey;
			}
			
			private function get highlightComponentEffect(): Effect {
				var glow: Glow = new Glow();
				glow.color = 0xFFFF00;
				glow.strength = 10;
				glow.inner = false;
				glow.duration = 1000;
				return glow;
			}
		]]>
	</mx:Script>
	<panels:VariablePanel id="variablePanel" height="100%" width="100%" />
	<view:FlexTramp id="tramp" width="100%" height="100%" />
</mx:VDividedBox>
